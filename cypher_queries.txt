MATCH (c:Compound {compound_id: "cpd00069"})-[sim:CHEMICAL_SIMILARITY]-(similar:Compound)
MATCH (similar)-[:PRODUCT_OF]->(r:Reaction)
WHERE toFloat(sim.distance) <= 0.2
RETURN r.reaction_id AS reaction_id,
       r.name AS reaction_name,
       similar.compound_id AS similar_compound_id,
       similar.name AS similar_compound_name,
       similar.smiles AS similar_compound_smiles,
       toFloat(sim.distance) AS similarity_distance
ORDER BY similarity_distance ASC
LIMIT 1000


MATCH (c:Compound {compound_id: $compound_id})
OPTIONAL MATCH (c)-[r:PRODUCT_OF]-(reaction:Reaction)
RETURN c, r, reaction
LIMIT 1000


MATCH (p:Protein)-[:CATALYZES]->(r:Reaction)
WHERE $ec_number IN p.ec_numbers
OPTIONAL MATCH (m:Genome)-[:CONTAINS]->(p)
OPTIONAL MATCH (m)-[:ORIGINATED_FROM]->(s:Sample)
WHERE
   ($min_temp IS NULL OR toFloat(s.temperature) >= $min_temp) AND
   ($max_temp IS NULL OR toFloat(s.temperature) <= $max_temp) AND
   ($min_lat IS NULL OR toFloat(s.latitude) >= $min_lat) AND
   ($max_lat IS NULL OR toFloat(s.latitude) <= $max_lat) AND
   ($min_lon IS NULL OR toFloat(s.longitude) >= $min_lon) AND
   ($max_lon IS NULL OR toFloat(s.longitude) <= $max_lon)
RETURN
   p.protein_id AS protein_id,
   p.name AS protein_name,
   r.reaction_id AS reaction_id,
   r.name AS reaction_name,
   m.genome_id AS genome_id,
   s.biosample_id AS sample_id,
   s.temperature AS sample_temperature,
   s.latitude AS sample_latitude,
   s.longitude AS sample_longitude
LIMIT $limit